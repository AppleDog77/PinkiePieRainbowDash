//Separate from the menus because you could put other interactions here, like poking or booping or punching or asking about various hotspots
OnMouseDoubleClick
{
	if reference5 == 0 //reference5 is the mouse button being clicked, in this case a left click
	{
		//reference3 is which character is being clicked
		//If you want to add double click interactions, reference4 will be the name of the hitbox the mouse is over
		if reference3 == 0
		{
			if reference4 =="Head" || reference4 == "Face"
			{		
			"\0\s[0]Hehe, aww thanks for kissing me, but why? Teehee.\w8\1\s[10]Bahahahaha!\w8\0\s[0]\n\n[half]Hey come on!\e"
			"\0\s[0]*smooch*\e"
			}
			else; OnSakuraMenu
		}
		else
		{				
			if reference4 =="Head" || reference4 == "Face"
			{		
			"\1\s[10]*blushes*\e"
			"\1\s[10]Another one of my adoring fans...\e"
			}
			else; OnKeroMenu
		}
	}
}

OnMouseMove
{
	if reference4 != ""
	{
		stroke++
		
		if stroke % 40 == 0
		{
			if reference3 == 0; OnMouseMove.Sakura
			else; OnMouseMove.Kero
		}
	}
}

OnMouseMove.Sakura
{
    continuousstroke++

    if reference4 == "Head"
    {
        if continuousstroke >= 40 
        {
            if stroke >= 40 
            {
                stroke = 0
                totalpets++
                currentpetstreak++
                "\0\s[0]Ooh, you're petting me a lot!\e"
                "\0\s[0]Ooh, you sure are petting me a lot!\e"
                "\0\s[0]Ahh...\e"
                "\0\s[0]Aww, thanks!\e"
                "\0\s[0]*purrs*\e"
            }
        }
        elseif stroke >= 20
        {
            stroke = 0 //Stroke value is reset
            totalpets++
            currentpetstreak++
            "\0\s[0]Ahh, nice.\e"
			"\0\s[0]Aww, thanks!\e"
			"\0\s[0]*purrs*\e"
        }
    }
    elseif reference4 == "Face"
    {
        if continuousstroke >= 40 
        {
            if stroke >= 40
            {
                stroke = 0
                totalpets++
                currentpetstreak++ 
                "\0\s[0]Ooh, you're booping me a lot!\e"
                "\0\s[0]Ooh, you sure are booping me a lot!\e"
                "\0\s[0]Thanks for all the boops!\e"
                "\0\s[0]Great!\e"
                "\0\s[0]Cool!\e"
            }
        }
        elseif stroke >= 20
        {
            stroke = 0
            "\0\s[0]Thanks for the boops!\e"
			"\0\s[0]Great!\e"
 			"\0\s[0]Cool!\e"
        }
    }
}

OnMouseMove.Kero
{
    continuousstroke++

    if reference4 == "Head"
    {
        if continuousstroke >= 40 //this would happen starting at 4 pets. Adjust as needed, but you should probably keep it as a multiple of whatever your normal stroking dialogue triggers at
        {
            if stroke >= 40 //Stroke count is longer so that you have time to read the dialogue here. Adjust as you like.
            {
                stroke = 0
                totalpets++
                currentpetstreak++ //I haven't used this yet, but if you want to count successful pets for the high score rather than the raw stroke value, use this variable instead. More on this below.
                "\1\s[10]Aw yeah!\e"
                "\1\s[10]Boy, you sure are headrubbing me a lot!\e"
            }
        }
        elseif stroke >= 20
        {
            stroke = 0 //Stroke value is reset
            totalpets++
            currentpetstreak++
            "\1\s[10]Ahh... nice... thanks!\w8\0\s[0]Cool, Dashie! I didn't know you liked headrubs. Although it makes sense since you like going to the spa-\w8\1\s[10]\n\n[half]Hey! Can't a pony have some privacy? No more headrubs, please. Pinkie will see!\e"
			"\1\s[10]Ahh... nice... thank you!\e"
			"\1\s[10]Awesome!\e"
			"\1\s[10]Cool!\e"
        }
    }
    elseif reference4 == "Face"
    {
        if continuousstroke >= 40 //this would happen starting at 4 pets. Adjust as needed, but you should probably keep it as a multiple of whatever your normal stroking dialogue triggers at
        {
            if stroke >= 40 //Stroke count is longer so that you have time to read the dialogue here. Adjust as you like.
            {
                stroke = 0
                totalpets++
                currentpetstreak++ //I haven't used this yet, but if you want to count successful pets for the high score rather than the raw stroke value, use this variable instead. More on this below.
                "\1\s[10]Aw yeah!\e"
                "\1\s[10]Boy, you sure are booping me a lot!\e"
            }
        }
        elseif stroke >= 20
        {
            stroke = 0
            "\1\s[10]Boop!\e"
        }
    }
}

OnMouseLeaveAll
{
    if continuousstroke > 40 && continuousstroke > pethighscore 
    {
        pethighscore = continuousstroke
        "\C\n\nWoo, new high score! (%(pethighscore)!)\e"
    }
    --
    if currentpetstreak > toppetstreak 
    {
        toppetstreak = currentpetstreak
        //"\C\n\nWoo, new high score! (%(toppetstreak) pets!)\e"
    }
    --
    stroke = 0
    continuousstroke = 0
    currentpetstreak = 0
}