//---------------------------Mouse Functions--------------------------
//As you've probably guessed, this .dic file has to do with mouse functions. This includes the petting feature and what happens when you double click your ghost.
//As a note, this .dic file isn't strictly necessary - I'm pretty sure you can just clear the file and it won't hurt your ghost any, it'll just make it so you can't pet or double click them. And if you're interested in only petting or double clicking one character, all you have to do is delete the chunks of code for the other character you want to be uninvolved. However, I'm assuming most people making ghosts want to make their ghosts pettable and clickable, so this will cover the messages for both.

//Make sure to read the walkthrough page (http://www.ashido.com/ukagaka/walkthrough.html) for more details about how to properly format ghost dialogue!
//I will reiterate that you should be editing these in Notepad++, and in particular, you should set the Language to C (or Java I guess) to properly highlight all the text. It will make things A LOT EASIER for you, trust me! It will also help you keep track of your brackets in case you forget some. If you click on a line with a bracket in Notepad++, it should highlight where the other bracket ends, or turn purple if there's no finishing bracket (which you should definitely fix!)
//It will also highlight my commented lines in green, so you'll know where they are. Any line beginning with // is a commented line that the code will not read. It's just for your reference!


//--OnMouseMove
//This is a bunch of code determining who the mouse is over. Don't touch any of it.

OnMouseMove
 {
  	if nowmode != "QUIZ"
 	{
		//---- reference3 determines the character in question
		if reference3 == 0 //if the character is 0, aka the main character
		{
			//---- it will run the mouse functions for that character
			MouseMoveSakura
		}
		else //if the character isn't 0, then it must be the side character.
		{
			//---- this will run the mouse functions for the side character.
			MouseMoveKero
		}
	}
 }
 
 //*********************Advanced User Info*******************************
//These text boxes are meant for those who have an interest in expanding or using more complicated functions in their ghost. Also for my own reference in the future since I plan on using this template too, haha. If you have no interest in any of this and just want to keep things as simple as possible, feel free to skip these boxes.

//If you want to have more than two characters at any point, like for example how I added Charger with my Hunter/Smoker ghost, and you want to be able to interact with that character like you can with the others, you will have to add in that character to OnMouseMove above. You'll need to know the p[] you've assigned your third character to, which in Charger's case was 3. You can assign your third character any p you want, just be consistent about using the same p[] everytime. I touch on \p a bit more in the walkthrough.
//So anyway, as you can see above, OnMouseMove determines who the user is touching, so you'll just have to add in an elseif checking for reference3 == 3, or reference3 == 4, or whatever p number is you're using. More on elseif in the walkthrough and in bootend.dic. After that, you'll just need to copy and paste the other mouse functions below and change the name as you need (for example, MouseMoveCharger, MouseDoubleClickCharger). 

//Note though that the extra character will not be a hard-coded character like 0 and 1! You're basically tricking the program into thinking there's another character, and some functions in things like menu.dic and aitalk.dic won't work quite right if you put in a value other than 0 or 1, and you'll need to be careful about making sure to remove the third character when you're done. But luckily for you, this won't be a problem with any of the mouse functions as far as I'm aware, and you can fill them out for the third character just like you would for the two main ones.
//***********************************************************************


 //--MouseMoveSakura
 //The petting function for your main character. Remember how it was referred to as sakura in some of the descript.txt and install.txt files?

 //Written by Zichqec https://zichqec.github.io/s-the-skeleton/
 //I've redone the petting code from the GT template, since some of it didn't actually do what it claimed to, at least in my testing. With this system, we track two stroke values, one for normal petting which resets every time you trigger dialogue, and one for continuous petting that counts up until the mouse leaves the character. For this to work, it's vital that you use either OnMouseLeave or OnMouseLeaveAll to reset the stroke values when the mouse leaves the character. The way the template had this set up doesn't actually work, so it's important you add the new event.

MouseMoveSakura
{
    stroke++ //These increase the stroke values by 1 any time the mouse is moved
    continuousstroke++

    if reference4 == "Head"
    {
        if continuousstroke >= 40 //this would happen starting at 4 pets. Adjust as needed, but you should probably keep it as a multiple of whatever your normal stroking dialogue triggers at
        {
            if stroke >= 40 //Stroke count is longer so that you have time to read the dialogue here. Adjust as you like.
            {
                stroke = 0
                totalpets++
                currentpetstreak++ //I haven't used this yet, but if you want to count successful pets for the high score rather than the raw stroke value, use this variable instead. More on this below.
                "\0\s[0]Ooh, you're petting me a lot!\e"
                "\0\s[0]Ooh, you sure are petting me a lot!\e"
                "\0\s[0]Ahh...\e"
                "\0\s[0]Aww, thanks!\e"
                "\0\s[0]*purrs*\e"
            }
        }
        elseif stroke >= 20
        {
            stroke = 0 //Stroke value is reset
            totalpets++
            currentpetstreak++
            "\0\s[0]Ahh, nice.\e"
			"\0\s[0]Aww, thanks!\e"
			"\0\s[0]*purrs*\e"
        }
    }
    elseif reference4 == "Face"
    {
        if continuousstroke >= 40 //this would happen starting at 4 pets. Adjust as needed, but you should probably keep it as a multiple of whatever your normal stroking dialogue triggers at
        {
            if stroke >= 40 //Stroke count is longer so that you have time to read the dialogue here. Adjust as you like.
            {
                stroke = 0
                totalpets++
                currentpetstreak++ //I haven't used this yet, but if you want to count successful pets for the high score rather than the raw stroke value, use this variable instead. More on this below.
                "\0\s[0]Ooh, you're booping me a lot!\e"
                "\0\s[0]Ooh, you sure are booping me a lot!\e"
                "\0\s[0]Thanks for all the boops!\e"
                "\0\s[0]Great!\e"
                "\0\s[0]Cool!\e"
            }
        }
        elseif stroke >= 20
        {
            stroke = 0
            "\0\s[0]Thanks for the boops!\e"
			"\0\s[0]Great!\e"
 			"\0\s[0]Cool!\e"
        }
    }
}

OnMouseLeaveAll //Activates when the mouse leaves all hitboxes. You can change to OnMouseLeave if you want it to happen when you leave one hitbox for another
{
    if continuousstroke > 40 && continuousstroke > pethighscore //Checks if you're in continuous petting, and if you are, if you got a high score. \C will display the previous balloon again so you can add onto it seamlessly
    {
        pethighscore = continuousstroke
        "\C\n\nWoo, new high score! (%(pethighscore)!)\e"
    }
    --
    if currentpetstreak > toppetstreak //Same thing here, but it's counting the number of actual pets you got. You can use this instead of the stroke count if you like.
    {
        toppetstreak = currentpetstreak
        //"\C\n\nWoo, new high score! (%(toppetstreak) pets!)\e"
    }
    --
    stroke = 0
    continuousstroke = 0
    currentpetstreak = 0
}

 //---- MouseMoveKero -----------------------------------------------------------------
 //MouseMoveKero is exactly like the above, except set up for your second character. 

MouseMoveKero
{
    stroke++ //These increase the stroke values by 1 any time the mouse is moved
    continuousstroke++

    if reference4 == "Head"
    {
        if continuousstroke >= 40 //this would happen starting at 4 pets. Adjust as needed, but you should probably keep it as a multiple of whatever your normal stroking dialogue triggers at
        {
            if stroke >= 40 //Stroke count is longer so that you have time to read the dialogue here. Adjust as you like.
            {
                stroke = 0
                totalpets++
                currentpetstreak++ //I haven't used this yet, but if you want to count successful pets for the high score rather than the raw stroke value, use this variable instead. More on this below.
                "\1\s[10]Aw yeah!\e"
                "\1\s[10]Boy, you sure are headrubbing me a lot!\e"
            }
        }
        elseif stroke >= 20
        {
            stroke = 0 //Stroke value is reset
            totalpets++
            currentpetstreak++
            "\1\s[10]Ahh... nice... thanks!\w8\0\s[0]Cool, Dashie! I didn't know you liked headrubs. Although it makes sense since you like going to the spa-\w8\1\s[10]\n\n[half]Hey! Can't a pony have some privacy? No more headrubs, please. Pinkie will see!\e"
			"\1\s[10]Ahh... nice... thank you!\e"
			"\1\s[10]Awesome!\e"
			"\1\s[10]Cool!\e"
        }
    }
    elseif reference4 == "Face"
    {
        if continuousstroke >= 40 //this would happen starting at 4 pets. Adjust as needed, but you should probably keep it as a multiple of whatever your normal stroking dialogue triggers at
        {
            if stroke >= 40 //Stroke count is longer so that you have time to read the dialogue here. Adjust as you like.
            {
                stroke = 0
                totalpets++
                currentpetstreak++ //I haven't used this yet, but if you want to count successful pets for the high score rather than the raw stroke value, use this variable instead. More on this below.
                "\1\s[10]Aw yeah!\e"
                "\1\s[10]Boy, you sure are booping me a lot!\e"
            }
        }
        elseif stroke >= 20
        {
            stroke = 0
            "\1\s[10]Boop!\e"
        }
    }
}

//---- OnMouseDoubleClick -------------------------------------------------------
//This is set up much like OnMouseMove above, except this is checking to see if the user is double clicking on the ghost. You also don't need to touch any of this. 
//Advanced users adding a third character, the same principles from OnMouseMove apply here as well! Make sure to add an elseif with the right number for your third character.

OnMouseDoubleClick
 {
	//---- like with the OnMouseMove function above, this determines who the user is clicking on.
	
 	if reference3 == 0 //if the character is 0, aka your main character
	{
		MouseDoubleClickSakura
 	}
	else
 	{
 		MouseDoubleClickKero
	}
 }

 //---- MouseDoubleClickSakura ----------------------------------------------------------
 //Like the petting animation above, this is the reactions they have to being double clicked. These also rely on the collisions you set up in surfaces.txt! This set is specifically for the main character, as indicated by the Sakura in the name again.
 
 //*********************Advanced User Info*******************************
 //As a note, double clicking on a certain area of your ghost doesn't have to be the user hitting them. You could instead set up a collision for a piece of clothing, and then have the character talk about that bit of clothing when it's double clicked, for example. One of Smoker's double clicks gives her a high-five when she's in a certain pose. Maybe double clicking is patting them, or pointing behind them, or tapping them, or any number of things. You can be creative here with how they respond depending on what areas you've set up as collisions!
 
 //If you don't want your character to be hittable at all, you can just delete everything in MouseDoubleClickSakura except OpenSakuraMenu. So it'd look like
 
 //MouseDoubleClickSakura
 //{
 //OpenSakuraMenu
 //}
 
 //That's all there is to it. This method also applies to the secondary character, Kero, below this set.
 //***********************************************************************
 
MouseDoubleClickSakura
 {
 	if reference4 == "Head" || reference4 == "Face" //here, this is checking to see if the user is double clicking on either the character's head OR their face, which for our purposes means they're kissing the ghost. More about operators at the walkthrough.
	{
		"\0\s[0]Hehe, aww thanks for kissing me, but why? Teehee.\w8\1\s[10]Bahahahaha!\w8\0\s[0]\n\n[half]Hey come on!\e"
		"\0\s[0]*smooch*\e"
		//Replace this dialogue with appropriate dialogue as you need. You can add more or cut it down if you want, even down to nothing if you want no response from your ghost at all when those areas are double clicked.
 	}
	else
 	{
		//Otherwise, if the user doubleclicks somewhere else on the character, they will open their menu as detailed in menu.dic.
 		OpenSakuraMenu
	}
 }

 
 //---- MouseDoubleClickKero -----------------------------------------------------------
 //This is just the same as MouseDoubleClickSakura above, except for your secondary character. The same principles apply, including how to make your character unhittable.
 
 
MouseDoubleClickKero
 {
 	if reference4 == "Head" || reference4 == "Face" //Triangle doesn't have a separate head really, being a triangle, so this only happens if you doubleclick his face. But since Rainbow Dash DOES have a head, being a pony...
	{
		"\1\s[10]*blushes*\e"
		"\1\s[10]Another one of my adoring fans...\e"
		//Replace the dialogue here for the second character with whatever you want. What I said above about adding or trimming still holds here.
 	}
	else //Otherwise if you click anywhere else on Triangle's body
 	{
		OpenKeroMenu //This will open his menu set up in menu.dic.
	}
 }

 //*********************Advanced User Info*******************************
 //There are other mouse related functions you could set up much like the above, such as OnMouseDragStart, OnMouseDragEnd, OnMouseWheel, and others which correspond to various things you can do with your mouse. You can probably guess what those functions do just by their names - OnMouseWheel would be if someone spun their mouse wheel over a certain area, for example. If you're interested in these, I'd recommend doing some reading at the CROW-SSP Encyclopedia to get a better idea of them and settle down for some trial and error learning as you try to put them in.
 //***********************************************************************
